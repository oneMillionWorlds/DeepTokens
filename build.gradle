
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

group = 'com.onemillionworlds'
version = '1.2.3'

test {
    useJUnitPlatform()
}


def jme3 = [v:'3.6.1-stable', g:'org.jmonkeyengine']

dependencies {

    /*
     * It is presumed any user of Deep tokens will already be using JMonkey but we don't want to force a version on them.
     * That is why this is a compileOnly dependency
     */
    compileOnly "${jme3.g}:jme3-core:${jme3.v}"
    compileOnly "${jme3.g}:jme3-lwjgl3:${jme3.v}"
    compileOnly "${jme3.g}:jme3-desktop:${jme3.v}"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation "${jme3.g}:jme3-core:${jme3.v}"
}

publishing { //see https://docs.gradle.org/current/userguide/publishing_maven.html
    publications {
        mavenJava(MavenPublication) {
            groupId = "${group}"
            artifactId = 'deeptokens'
            version = "${version}"

            from components.java

            pom {
                name = project.name
                description = 'A mesh building library for JMonkeyEngine to build extruded shapes from 2D images'
                url = 'https://github.com/oneMillionWorlds/deeptokens'
                licenses {
                    license {
                        name = 'New BSD (3-clause) License'
                        url = 'http://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                scm {
                    connection = 'git@github.com:oneMillionWorlds/DeepTokens.git'
                    developerConnection = 'git@github.com:oneMillionWorlds/DeepTokens.git'
                    url = 'https://github.com/oneMillionWorlds/DeepTokens'
                }
                developers {
                    developer {
                        id = 'RichardTingle'
                        name = 'Richard Tingle (aka richtea)'
                        email = 'support@oneMillionWorlds.com'
                    }
                }
            }
        }
    }
    repositories {
        // Local publishing for development/testing
        mavenLocal()

        // Local directory used to assemble a Central Publisher Portal bundle (staging area)
        // This keeps publishToMavenLocal working and avoids pushing to OSSRH directly in CI.
        maven {
            name = 'CentralPortalStaging'
            url = uri("${buildDir}/central-portal-staging")
        }

        maven {
            name 'sonatype'
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            //url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username findProperty('ossrhUsername') ?: ''
                password findProperty('ossrhPassword') ?: ''
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

// --- Central Publisher API integration (USER_MANAGED) ---
ext.centralPortalStagingDir = file("${buildDir}/central-portal-staging")

// Helper task alias with clearer name for staging to the local Central Portal directory
tasks.register('publishToCentralPortalStaging') {
    dependsOn 'publishMavenJavaPublicationToCentralPortalStagingRepository'
    group = 'publishing'
    description = 'Publishes the mavenJava publication to local Central Portal staging directory.'
}

// Zip the staged repository into a single bundle suitable for uploading to Central Portal
tasks.register('zipCentralPortalBundle', Zip) {
    group = 'publishing'
    description = 'Zips the Central Portal staging directory into build/central-bundle.zip'
    dependsOn 'publishToCentralPortalStaging'
    from(centralPortalStagingDir)
    archiveFileName = 'central-bundle.zip'
    destinationDirectory = file("${buildDir}")
}

// Convenience aggregate task
tasks.register('prepareCentralBundle') {
    group = 'publishing'
    description = 'Stages and zips artifacts ready for Central Publisher Portal upload (no network).'
    dependsOn 'zipCentralPortalBundle'
}
